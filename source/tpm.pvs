%% ----
%%
%% TPM Theory
%%
%% Author: Perry Alexander
%% Date: Mon Nov 28 01:09:39 CST 2011
%%
%% Description: Basic model of a TPM using a monadic state model
%% 
%% Dependencies:
%%  StateMonad.pvs
%%
%% Todo: (key - => pending, + => done)
%% + Added updates to locality following sinit measurement
%% + Added monotonicity proof for locality changes
%% - Add regressions and theorems for key installation
%% - outBlob should be outKey and only output keys
%% - Add PCR composite to wrapped keys
%% - Add commands for sealing small data to state
%% ----

tpm [ B:TYPE+, % BLOB
      HV:TYPE+,% Hash value
      hash:[B->HV], % Hash function
      K:TYPE+ % Key
     ] : THEORY

  BEGIN

  ASSUMING
    % Assume that different blobs always have different hashes
    unique_hash: ASSUMPTION
      FORALL (b0,b1:B) : hash(b0)=hash(b1) iff b0=b1;
  ENDASSUMING

  IMPORTING pcr[HV];
  IMPORTING key[B,K,HV];

  %% State monad input type.  All inputs are in the form of a command with
  %% parameters.
  tpmInput : DATATYPE
  BEGIN
    decryptKeyCom(d:(encrypt?)) : decryptKeyCom?
    encryptCom(en:BLOB) : encryptCom? % unimplemented
    %% Startup commands
    TPM_Init : TPM_Init? % TPM goes into a state where only TPM_startup
                         % is allowed.  This is a hardware signal and cannot
			 % be invoked by software
    TPM_Startup : TPM_Startup? % clear - all variables back to default values
                               % save - restore from TPM_SaveState
			       % deactivated - turn off and wait for TPM_init
    TPM_SaveState : TPM_SaveState? % See docs on how save occurs
    TPM_Extend(h:HV,i:PCRINDEX) : TPM_Extend?
    TPM_LoadKey2(lk:(wrapKey?)): TPM_LoadKey2? 
    noopCom : noopCom?
    offCom : offCom?
    TPM_Quote(nonce:BLOB,pm:PCRMASK) : TPM_Quote? % partially implemented
    revokeKeyCom(rk:(wrapKey?)): revokeKeyCom?
    senterCom : senterCom? % implemented all actions as one senter
    sinitCom : sinitCom? % partially implemented
    TPM_Unseal(s:(seal?),uk:(asymKey?)) : TPM_Unseal?   
    TPM_Seal(sk:(asymKey?),data:BLOB) : TPM_Seal?
    TPM_TakeOwnership : TPM_TakeOwnership?
    TPM_OwnerClear : TPM_OwnerClear? % unimplemented
    TPM_ForceClear : TPM_ForceClear? % unimplemented
    TPM_DisableOwnerClear : TPM_DisabelOwnerClear? % unimplemented
    TPM_CreateWrapKey(wk,parentk:(asymKey?)): TPM_CreateWrapKey?
  END tpmInput;

  %% State monad output type
  tpmAbsOutput : DATATYPE
  BEGIN
    outNothing : outNothing?
    outError(s:string) : outError?
    outNat(n:nat) : outNat?
    outQuote(qk:KEY,non:BLOB,pcrs:list[PCR]) : outQuote?
    outBlob(bl:BLOB) : outBlob?
    outKey(k:KEY) : outKey?
  END tpmAbsOutput;

  %% sinit blob instance for measurement
  sinit : B;

  %% mle blob instance for measurement
  mle : B;

  %% Locality type - check with spec
  LOCALITY : TYPE = {n:nat | n<=4};

  %% Uninterpreted Key values.
  ekKeyVal : K;
  srkKeyVal : K;

  %% Key definitions that make ek and srk values asymmetric keys.
  ekVal : KEY = asymKey(ekKeyVal);
  srkVal : KEY = asymKey(srkKeyVal);

  %% Abstract TPM State with keys, PCR array and locality
  tpmAbsState : TYPE = [#
                       postInit : bool,
                       srk : (asymKey?),
		       ek : (asymKey?),
		       keys : KEYSET,
                       pcrs : PCRS,
		       locality : LOCALITY
		    #];

  IMPORTING StateMonad[tpmAbsOutput,tpmAbsState];

  %% Define some common TPM states and state operations
  
  %% Unknown state
  tpmUnknown : tpmAbsState

  %% Power on state
  tpmPostInit : tpmAbsState = (#
                         pcrs:=pcrsPower
			 , postInit:=true
                         , locality:=4
			 , keys:=emptyset
			 , srk:=inverse(srkVal)
			 , ek:=inverse(ekVal)
			 #);

  %% Standard initial state following reset
  tpmStartup : tpmAbsState = (#
                         pcrs:=pcrsReset
			 , postInit:=false
			 , locality:=4
			 , keys:=emptyset
			 , srk:=inverse(srkVal)
			 , ek:=inverse(ekVal)
			 #);

  %% Predicate to determine if startup has occured
  afterInit?(s:tpmAbsState):bool = postInit(s);
  afterStartup?(s:tpmAbsState):bool = NOT postInit(s);

  %% Standard operations on TPM state definition above.  All such
  %% functions end with State to indicate they operate on the state
  %% value rather than on the TPM moand.  Note that key set and pcr
  %% manipulation functions are defined externally in key.pvs and
  %% pcr.pvs respectively

  %% Reset PCRs as performed by SENTER.
  pcrsResetSenterState(s:tpmAbsState) : tpmAbsState =
    s WITH [`pcrs := pcrsReset];

  %% Decrease locality value
  changeLocalityState(s:tpmAbsState) : tpmAbsState =
    s WITH [`locality := IF locality(s) > 0
                         THEN locality(s) - 1
			 ELSE 0
			 ENDIF];
  
  %% Extend operation on TPM state
  extendState(s:tpmAbsState,n:PCRINDEX,h:HV) : tpmAbsState =
    s WITH [`pcrs := pcrsExtend(pcrs(s),n,h)];

  %% Install key operation on TPM state.
  
  installKeyState(s:tpmAbsState,k:(wrapKey?)) : tpmAbsState =
    s WITH [`keys := addKey(k,srk(s),keys(s))];

  %% Revoke key operation on TPM state
  revokeKeyState(s:tpmAbsState,k:(wrapKey?)) : tpmAbsState =
    s WITH [`keys := removeKey(k,keys(s))];

  %% Decrypt a secret encrypted with a wrapped key
  decryptKeyState(s:tpmAbsState,d:(encrypt?)) : BLOB =
    CASES key(d) OF
      wrapKey(w,k) : IF member(inverse(w),add(srk(s),keys(s)))
                        THEN blob(d)
			ELSE nothing ENDIF
      ELSE nothing
    ENDCASES;

  %% What we want to generate with each command is a pair of type
  %% [tpmAbsOut,tpmAbsState].  executeCom generates the the state
  %% from a TPM state and command while the upcoming outputCom
  %% generates the corresponding output.  Thus, to execute any
  %% operation, both executeCom and outputCom must be used

  %% Run if TPM_Init has run but TPM_Startup has not
  executeComPostInit(s:(afterInit?),c:tpmInput) : tpmAbsState =
    CASES c OF
      TPM_Startup : tpmStartup
      ELSE s
    ENDCASES;

  %% Run if TPM_Startup has run
  executeComStartup(s:(afterStartup?),c:tpmInput) : tpmAbsState = 
    CASES c OF
      TPM_Extend(h,n) : extendState(s,n,h),
      TPM_LoadKey2(k): installKeyState(s,k), 
      revokeKeyCom(k): revokeKeyState(s,k),
      noopCom : s,
      offCom : tpmUnknown,
      TPM_Init : tpmPostInit,
      TPM_Startup : s,
      senterCom : 
        changeLocalityState(extendState(pcrsResetSenterState(s),0,hash(sinit))),
      sinitCom : changeLocalityState(extendState(s,0,hash(mle))),
      TPM_Quote(n,pm) : s,
      decryptKeyCom(d) : s,
      TPM_Unseal(d,k) : s,
      TPM_Seal(k,data) : s,
      TPM_TakeOwnership : s,
      TPM_CreateWrapKey(k,parent): s
      ELSE s
  ENDCASES;
  
  %% Execute a command on state generating a new state.  Commands execution
  %% should be blocked if startup has not run and postInit=true.
  executeCom(s:tpmAbsState,c:tpmInput) : tpmAbsState =
    IF afterInit?(s)
       THEN executeComPostInit(s,c)
       ELSE executeComStartup(s,c)
    ENDIF;
                    
  % Generate a NEW RANDOM key pair.
  createWrapKeyOut(k,parent:(asymKey?)):tpmAbsOutput = 
     outKey(wrapKey(parent,k));

  % NEED TO ENSURE NEW, RANDOM SRK KEY PAIR
  takeOwnershipOut : tpmAbsOutput =
     outKey(srkVal);
  
  % Output PCRs from a state as quote
  quoteOut(s:tpmAbsState,n:BLOB,pm:PCRMASK) : tpmAbsOutput =
    outQuote(inverse(ekVal),n,getPCRs(s`pcrs,pm));

  % Output decypted key
  decryptKeyOut(s:tpmAbsState,d:(encrypt?)) : tpmAbsOutput = 
    outBlob(decryptKeyState(s,d));

  % Output secret unsealed with an asymetric key and PCRs 
  unsealOut(s:tpmAbsState,d:(seal?),k:(asymKey?)) : tpmAbsOutput = 
    outBlob(unseal(d,pcrs(s),k));

  % Output secret sealed with asymetric key 
  sealOut(s:tpmAbsState,k:(asymKey?),data:BLOB) : tpmAbsOutput = 
    outBlob(seal(k,pcrs(s),data));

  %% Generate output from a command and state
  outputCom(s:tpmAbsState,c:tpmInput) : tpmAbsOutput = 
    CASES c OF
      TPM_Extend(h,n) : outNothing,
      TPM_LoadKey2(k): outNothing, 
      revokeKeyCom(k): outNothing,
      noopCom : outNothing,
      offCom : outNothing,
      TPM_Init : outNothing,
      senterCom : outNothing,
      sinitCom : outNothing,
      TPM_Quote(n,pm) : quoteOut(s,n,pm),
      decryptKeyCom(d) : decryptKeyOut(s,d),
      TPM_Unseal(d,k) : unsealOut(s,d,k),
      TPM_Seal(k,data) : sealOut(s,k,data),
      TPM_TakeOwnership : takeOwnershipOut,
      TPM_CreateWrapKey(k,parent) : createWrapKeyOut(k,parent)
      ELSE outNothing
  ENDCASES;

  %% Monotonicity of locality
  %% Assuming that we're not resetting or powering on, locality goes down
  %% or remains the same
  %% unproven - Wed Jun 13 23:15:36 CDT 2012
  monotonic_locality: THEOREM (FORALL (s:tpmAbsState,c:tpmInput) :
    not(offCom?(c) or TPM_Init?(c)) =>
      locality(s) >= locality(executeCom(s,c)));

  %% Minor Support Lemmas

  %% Extend is antisymmetric if the hash values used for extension are
  %% not equal.  In this example we use the LET form to define
  %% commands directly with monad functions defined in StateMonad.pvs
  antisymmetryMonad: THEOREM (FORALL (hv0,hv1:HV):
    LET f1:State =
          modify(outNothing,
                 (LAMBDA (s:tpmAbsState):s WITH [`pcrs:=pcrsExtend(pcrs(s),0,hv0)])),
      	f2:State =
          modify(outNothing,
                 (LAMBDA (s:tpmAbsState):s WITH [`pcrs:=pcrsExtend(pcrs(s),0,hv1)])) IN
      hv0/=hv1 =>
        runState(
          f2 >>= (LAMBDA (x:tpmAbsOutput): f1 >>= unit))(tpmStartup)
        /=
        runState(
          f1 >>= (LAMBDA (x:tpmAbsOutput): f2 >>= unit))(tpmStartup))

  %% TPM Command Definitions - Note that all commands used by the TPM
  %% have the TPM suffix to distinguish from operations on tpm state.
  %% Commands thus far are defined as state transforming or output
  %% generating.  This does not exclude the eventuality of doing both.

  %% Extend PCR n with hash value h.
  extendTPM(h:HV,n:PCRINDEX):State =
    modify(outNothing,
           (LAMBDA (s:tpmAbsState):executeCom(s,TPM_Extend(h,n))));

  %% Decrease locality value
  changeLocalityTPM : State =
    modify(outNothing,
           (LAMBDA (s:tpmAbsState):changeLocalityState(s)));

  %% Install key k in a TPM
  installKeyTPM(k:(wrapKey?)):State =
    modify(outNothing,
           (LAMBDA (s:tpmAbsState):executeCom(s,TPM_LoadKey2(k))));

  %% Revoke key k from a TPM.
  revokeKeyTPM(k:(wrapKey?)):State =
    modify(outNothing,
           (LAMBDA (s:tpmAbsState):executeCom(s,revokeKeyCom(k))));

  %% No-op
  noop : [tpmAbsOutput -> State] =
    (LAMBDA (a:tpmAbsOutput): state((LAMBDA(s:tpmAbsState) : (a,s))));

  %% TPM is off
  offTPM : State = put(outNothing,tpmUnknown);

  %% Power up the TPM
  powerTPM : State = put(outNothing,tpmPostInit);

  %% Reset the TPM by senter call
  senterResetTPM : State = put(outNothing,tpmStartup);

  %% Measure sinit by senter call
  senterHashSinitTPM : State = extendTPM(hash(sinit),0);

  %% Call sinit for initial measurements - currently unused
  sinitTPM : State = extendTPM(hash(mle),0);
  
  %% Establish SRK
  takeOwnershipTPM : State = 
     output(outNothing,
            (LAMBDA (s:tpmAbsState) : outputCom(s,TPM_TakeOwnership)));

  %% Wrap new key k with parent key
  createWrapKeyTPM(k,parent:(asymKey?)) : State = 
     output(outNothing,
            (LAMBDA (s:tpmAbsState) : outputCom(s,TPM_CreateWrapKey(k,parent))));
		
  %% Generate and output a quote
  quoteTPM(n:BLOB,pm:PCRMASK) : State = 
     output(outNothing,
            (LAMBDA (s:tpmAbsState) : outputCom(s,TPM_Quote(n,pm))));

  %% Decrypt and output a blob (should be a key)
  decryptTPM(d:(encrypt?)) : State =
     output(outNothing,
            (LAMBDA (s:tpmAbsState) : outputCom(s,decryptKeyCom(d))));

  %% Unseal and output a blob (should be binary or data)
  unsealTPM(d:(seal?),k:(asymKey?)) : State = 
     output(outNothing,
            (LAMBDA (s:tpmAbsState): outputCom(s,TPM_Unseal(d,k))));

  %% Seal and output a blob (should be binary or data)
  sealTPM(k:(asymKey?),data:BLOB) : State = 
     output(outNothing,
            (LAMBDA (s:tpmAbsState): outputCom(s,TPM_Seal(k,data))));

  %% Extending a reset PCR is antisymmetric if the two values are not equal
  %% Let form used to defined commands for bind.  Will use command forms
  %% later in antisymmetryMonad3
  antisymmetryMonad2: THEOREM (FORALL (hv0,hv1:HV) :
    LET 
        f1:State = extendTPM(hv0,0),
      	f2:State = extendTPM(hv1,0) IN
      (hv0/=hv1 =>
        runState(f2 >> f1 >>= unit)(tpmStartup)
        /=
        runState(f1 >> f2 >>= unit)(tpmStartup)))


  %% Extending a reset PCR is antisymmetric if the two values are not equal.
  %% No let form - commands appear directly in bind
  %% Theorem is now false when PCRs other than PCR0 are included.
  antisymmetryMonad3: THEOREM (FORALL (hv0,hv1:HV) :
    hv0/=hv1 =>
       runState(
         extendTPM(hv1,0)
	 >> extendTPM(hv0,0)
	 >>= unit)
	 (tpmStartup)
       /=
       runState(
         extendTPM(hv0,0)
	 >> extendTPM(hv1,0)
	 >>= unit)
	 (tpmStartup))

  %% Prove that senter after power up results in reset PCRs followed by sinit
  %% measurement.
  %% proved - Tue Jun 12 15:31:12 CDT 2012
  resetMonad: THEOREM (FORALL (hv:HV) :
    runState(
      powerTPM
      >> senterResetTPM
      >> senterHashSinitTPM
      >> changeLocalityTPM
      >>= unit)
      (tpmUnknown)
    =
    (outNothing,changeLocalityState(extendState(tpmStartup,0,hash(sinit)))))

  %% Prove that quote generation returns the correct PCR
  %% proved - Tue Jun 12 15:33:33 CDT 2012
  quoteMonad: THEOREM (FORALL (hv:HV,n:BLOB) :
    runState(
      powerTPM
      >> senterResetTPM
      >> senterHashSinitTPM
      >> changeLocalityTPM
      >> extendTPM(hv,0)
      >> quoteTPM(n,cons(0,null))
      >>= unit)
      (tpmUnknown)
    =
    (outQuote(inverse(ekVal),n,cons(extend(extend(reset,hash(sinit)),hv),null)),
      changeLocalityState(extendState(extendState(tpmStartup,0,hash(sinit)),0,hv))))

 
  %% If nonces don't match, quotes don't match
  %% proved - Fri Jun 22 16:55:16 CDT 2012
  badNonce: THEOREM (FORALL(n1,n2:BLOB, pm:PCRMASK, s:tpmAbsState) :
     n1/=n2 =>
       runState(
         quoteTPM(n1,pm))
	 (s)
       /=
       runState(
	 quoteTPM(n2,pm))
	 (s));
  
  %% Bad Signing Key - Man in the Middle Attack
  %% proved - Fri Jun 22 16:55:56 CDT 2012
  badSigningKey: THEOREM (FORALL(n:BLOB, pm:PCRMASK, k:(asymKey?), s:tpmAbsState) :
     LET(a,s) = runState(quoteTPM(n,pm))(s) IN
     inverse(k) /= inverse(ekVal) 
     =>
     outQuote(inverse(k),n,getPCRs(s`pcrs,pm))
     /=
     a);
     
  %% Output after going through tpm commands is same as grabbing pcrs
  %% proved - Fri Jun 22 16:56:08 CDT 2012
  checkPCRS: THEOREM (FORALL(hv:HV,pm:PCRMASK,n:BLOB) :
    LET (a,s) =
    runState(
      powerTPM
      >> senterResetTPM
      >> senterHashSinitTPM
      >> changeLocalityTPM
      >> extendTPM(hv,0)
      >> quoteTPM(n,pm)
      >>= unit)
      (tpmUnknown) IN
    a
    =
    outQuote(inverse(ekVal),n,map(s`pcrs,pm)));

  %% Checks three parts of outQuote (key, nonce, pcrs)
  %checkQuote: THEOREM(badNonce AND badSigningKey AND checkPCRS);

  %% Prove that skipping SENTER is detectable by showing the quote following
  %% a command differs when SENTER is excluded.
  %% proved - Tue Jun 12 15:34:24 CDT 2012
  noSenter: THEOREM (FORALL (hv:HV,n:BLOB,pcrMask:PCRMASK) :
    runState(
      powerTPM
      >> senterResetTPM
      >> senterHashSinitTPM
      >> changeLocalityTPM
      >> extendTPM(hv,0)
      >> quoteTPM(n,pcrMask)
      >>= unit)
      (tpmUnknown)
    /=
    runState(
      powerTPM
      >> extendTPM(hv,0)
      >> quoteTPM(n,pcrMask)
      >>= unit)
      (tpmUnknown))

  %% Decryption will fail if no keys are installed
  %% Now proved.  Problem was with grind, not pvs generally.
  %% proved - Tue Jun 12 15:36:53 CDT 2012  
  decrypt_key_fail: THEOREM FORALL (k:(wrapKey?),b:BLOB) :
    LET (out,s) = runState(
                  powerTPM
                  >> senterResetTPM
                  >> senterHashSinitTPM
		  >> changeLocalityTPM
                  >> decryptTPM(encrypt(k,b))
                  >>= unit)
                  (tpmUnknown) IN
      inverse(wrappingKey(k))/=inverse(srkVal) => out = outBlob(nothing);

  %% Key chaining theorem.  If k is wrapped with srk and encrypts b,
  %% b will decrypt if k is installed
  %% proved - Tue Jun 12 15:37:29 CDT 2012
  decrypt_key_success: THEOREM FORALL (k:(wrapKey?),b:BLOB):
    LET (out,s) = runState(
                  powerTPM
	  	  >> senterResetTPM
		  >> senterHashSinitTPM
		  >> changeLocalityTPM
		  >> installKeyTPM(k)
		  >> decryptTPM(encrypt(k,b))
		  >>= unit)
		  (tpmPostInit) IN
        inverse(wrappingKey(k))=srk(s) => out=outBlob(b)


  % A key is installed if it is wrapped with SRK
  % proved - Tue Jun 12 15:37:56 CDT 2012
  install_key_success: THEOREM FORALL (k:(wrapKey?)) :
    LET (out,s) = runState(
                  powerTPM
		  >> senterResetTPM
		  >> senterHashSinitTPM
		  >> changeLocalityTPM
		  >> installKeyTPM(k)
		  >>= unit)
		  (tpmUnknown) IN
      wrappingKey(k) = srk(s) => member(key(k),keys(s));

  %% A key is installed if it is wrapped with an installed key
  %% proved - Tue Jun 12 15:38:22 CDT 2012
  install_key_success2: THEOREM FORALL (j,k:(wrapKey?)) :
    LET (out,s) = runState(
                  powerTPM
		  >> senterResetTPM
		  >> senterHashSinitTPM
		  >> changeLocalityTPM
		  >> installKeyTPM(k)
		  >> installKeyTPM(j)
		  >>= unit)
		  (tpmUnknown) IN
      wrappingKey(j) = key(k) AND wrappingKey(k) = srk(s) =>
         member(key(j),keys(s));

  %% Unseal a secret successfully - This is not right yet 
  unseal_secret_success: THEOREM FORALL (j,k:(asymKey?),hv:HV,b:BLOB,pcrs:PCRS) :
    LET (out,s) = runState(
                  powerTPM
		  >> senterResetTPM
		  >> senterHashSinitTPM
		  >> changeLocalityTPM
		  >> extendTPM(hv,0)
		  >> unsealTPM(seal(k,pcrs,b),j)
		  >>= unit)
		  (tpmUnknown) IN
      IF j=inverse(k) AND pcrs=pcrs(s)
         THEN out=outBlob(b)
         ELSE out=outBlob(nothing)
      ENDIF;

  %% Seal a secret successfully
  seal_secret_success: THEOREM FORALL (j,k:(asymKey?),hv:HV,b:BLOB,s:tpmAbsState) :
    LET (out,s) = runState(
     	 	  powerTPM
 		  >> senterResetTPM
 		  >> senterHashSinitTPM
 		  >> changeLocalityTPM
 		  >> extendTPM(hv,0)
 		  >> sealTPM(k,b)
		  >>= unit)
 		  (tpmUnknown) IN
      j = inverse(k) =>
      unseal(bl(out),s`pcrs,j) = b;
  

  %% Regression theorems for testing only

  test1a: THEOREM (unit(outNat(3))) =
           (state(LAMBDA (s:tpmAbsState) : (outNat(3),s)))
  test1b: THEOREM (runState(unit(outNat(3)))) =
           (runState(state(LAMBDA (s:tpmAbsState) : (outNat(3),s))))
  test1c: THEOREM (runState(unit(outNat(3))))(tpmStartup) =
           (runState(state(LAMBDA (s:tpmAbsState) : (outNat(3),s))))(tpmStartup)

  test2:  THEOREM (runState(unit(outNat(3))))(tpmStartup) = (outNat(3),tpmStartup)

  % Random hash values
  hv0, hv1, hv2 : HV

  % reset followed by a copy
  test3: THEOREM
    runState(modify(outNat(0),(LAMBDA (s:tpmAbsState):s)) >>= unit)(tpmStartup)
    =
    (outNat(0),tpmStartup)

  % reset followed by two copies
  test4: THEOREM
    runState(
       modify(outNat(0),(LAMBDA (s:tpmAbsState):s)) >>=
       (LAMBDA (x:tpmAbsOutput):
         modify(outNat(1),(LAMBDA (s:tpmAbsState):s)) >>= unit))(tpmStartup)
     =
    (outNat(1),tpmStartup)

  % Take the output of the previous command execution and attempt to install
  % it as akey.  Note the use of useOutput to create a function for bind
  % rather than sequence.  The big difference is that the output from the
  % previous command executing is bound to a and can be used in the
  % morphism.  Note also that outKey? and wrapKey? that would usually be
  % used as predicate subtypes cannot be as we'll never guarantee the
  % output argument will be a key or a wrapped key.  This does not complicate
  % the proof, so it's not a big deal.
  installPrevKeyTPM:[tpmAbsOutput->State] =
    useOutput(outNothing,
               (LAMBDA (a:tpmAbsOutput):
                  (LAMBDA (s:tpmAbsState):
		    IF outKey?(a) AND wrapKey?(k(a))
		    THEN executeCom(s,TPM_LoadKey2(k(a)))
		    ELSE executeCom(s,noopCom)
		    ENDIF)));
                 
  
  % Generate a key and immediately install it.  Note the use of bind rather
  % sequence to capture the createWrapKeyTPM output and use it as input to
  % the installPrevKeyTPM command.  Specifically, the output of
  % createWrapKeyTPM is bound to a in installPrevKeyTPM then used in that
  % command.  Classic use of bind!!
  install_prev_key_success: THEOREM FORALL (k,p:(asymKey?)) :
    LET (out,s) = runState(
                  powerTPM
		  >> senterResetTPM
                  >> createWrapKeyTPM(k,p) 
		  >>= installPrevKeyTPM
		  >>= unit)
		  (tpmUnknown)
      IN member(p,keys(s)) OR p=srk(s)
       => member(k,keys(s));

  %%install nested wrapped keys?
  nested_wrapped_keys_success: THEOREM FORALL (k,j,p:(asymKey?)) :
     LET (out,s) = runState(
     	 	   powerTPM
		   >> senterResetTPM
		   >> senterHashSinitTPM
		   >> changeLocalityTPM
		   >> takeOwnershipTPM
		   >> createWrapKeyTPM(k,p)
		   >>= installPrevKeyTPM
		   >> createWrapKeyTPM(j,k)
		   >>= installPrevKeyTPM
		   >>= unit)
		   (tpmUnknown) IN
	member(p,keys(s)) OR p=srk(s)
	=>
	%% Does member(j,keys(s)) automatically imply member(k,keys(s))?
	%% member(j,keys(s)) pops out with grind, but I can't get the k part to pop out.
	member(j,keys(s)) AND member(k,keys(s));
	
  % Take the output of the previous command execution and attempt to unseal
  % it.  Note the use of useOutput to create a function for bind
  % rather than sequence.
  unsealPrevSealTPM(k:(asymKey?)):[tpmAbsOutput->State] =
    useOutput(outNothing,
		(LAMBDA (a:tpmAbsOutput):
		  (LAMBDA (s:tpmAbsState):
		    IF outBlob?(a) AND seal?(bl(a))
		    THEN executeCom(s, TPM_Unseal(bl(a),k))
		    ELSE executeCom(s,noopCom)
		    ENDIF)));

  %% Create a seal and immediately unseal it.
  unseal_prev_seal_success: THEOREM FORALL (k,j:(asymKey?),data:BLOB,pcrs:PCRS) :
    LET (out,s) = runState(
                  powerTPM
		  >> senterResetTPM
                  >> sealTPM(k,data)
		  >>= unsealPrevSealTPM(j)
		  >>= unit)
		  (tpmUnknown) IN  
	(j=inverse(k) AND pcrs=pcrs(s))
          => blob(bl(out)) = data
	 %ELSE out = outBlob(nothing)

  %% Seal and unseal a secret successfully 
  seal_unseal_secret_success: THEOREM FORALL (j,k:(asymKey?),hv:HV,b:BLOB,pcrs:PCRS) :
    LET (out,s) = runState(
                  powerTPM
		  >> senterResetTPM
		  >> senterHashSinitTPM
		  >> changeLocalityTPM
		  >> extendTPM(hv,0)
		  >> sealTPM(k,b)
		  >>= unsealPrevSealTPM(j)
		  >>= unit)
		  (tpmUnknown) IN
      (j=inverse(k) AND pcrs=pcrs(s))
	=> blob(bl(out))=b
     % ELSE out=outBlob(nothing)


END tpm
