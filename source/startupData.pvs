startupData[B,K,HV : TYPE+] : THEORY

  BEGIN

  IMPORTING pcr[HV];
  IMPORTING key[B,K,HV];
  IMPORTING PermanentFlags;
  IMPORTING PermanentData[B,K,HV];

  %% Flags used by the TPM_Startup command
  TPM_STARTUP_TYPE : DATATYPE
  BEGIN
    TPM_ST_CLEAR : TPM_ST_CLEAR?
    TPM_ST_STATE : TPM_ST_STATE?
    TPM_ST_DEACTIVATED : TPM_ST_DEACTIVATED?
  END TPM_STARTUP_TYPE  

  %% Record containing state data to be stored on TPM_SaveState invocation
  %% and restored when using TPM_ST_STATE flag.  The valid? flag indicates
  %% if whether the record is valid or not.  Note that EK and SRK should
  %% technically be in the permData structure, but we like to keep those
  %% values exposed at the TPM state level.  Most presentations of the TPM
  %% do this and do not reference permData or permFlags.  Thus, we try
  %% to be consistent here.
  restoreStateData : TYPE = [#
  		          valid? : bool,
                          srk : (asymKey?),
		          ek : (asymKey?),
			  keyGenCnt : K,
			  keys : KEYSET,
                          pcrs : PCRS,
			  permFlags : PermFlags,
			  permData : PermData
			#];

  wellFormedRestore?(r:restoreStateData) : bool =
    valid?(r) =>
    FORALL (i:PCRINDEX) :
      pcrReset(pcrAttrib(permData(r))(i)) => pcrs(r)(i) = resetOne;

  %% Create a save record with keys, pcrs, and pcr flags.  Throw out pcr
  %% values if they are resettable
  saveState(tpmKeys: KEYSET,
            tpmEK : (asymKey?),
	    tpmSRK : (asymKey?),
       	    tpmKeyGenCnt : K,
	    tpmPcrs : PCRS,
	    tpmPermFlags : PermFlags,
	    tpmPermData : PermData ) : (wellFormedRestore?) = 
    (# valid? := TRUE,
       keys := tpmKeys,
       ek := tpmEK,
       srk := tpmSRK,
       keyGenCnt := tpmKeyGenCnt,
       pcrs := (LAMBDA (i:PCRINDEX) :
                     IF pcrReset(pcrAttrib(tpmPermData)(i))
		        THEN resetOne
			ELSE tpmPcrs(i)
                     ENDIF),
       permFlags := tpmPermFlags,
       permData := tpmPermData
       #);

  %% Initial value for saved data.  The only field that matters is the valid?
  %% field that must be false.

  ekInit : K
  srkInit : K
  keyGenCntInit : K

  initSaveData : (wellFormedRestore?) =
    (# valid? := FALSE,
       keys := emptyset,
       ek := asymKey(ekInit),
       srk := asymKey(srkInit),
       keyGenCnt := keyGenCntInit,
       pcrs := pcrsPower,
       permFlags := permFlagsInit,
       permData := permDataInit
    #);

  END startupData


