%% ----
%%
%% startupData Theory
%%
%% Author: Perry Alexander, Brigid Halling
%% Date: 
%%
%% Description: 
%% 
%% Dependencies:
%%  PermanentData.pvs
%%  PermanentFlags.pvs
%%  StanyData.pvs
%%  StanyFlags.pvs
%%  StclearData.pvs
%%  StclearFlags.pvs
%%  key.pvs
%%  data.pvs
%%  keyData.pvs
%%  pcr.pvs
%%  authdata.pvs
%%
%% Todo: (key - => pending, + => done)
%% ----

startupData[K,HV:TYPE+] : THEORY

  BEGIN

  IMPORTING PermanentFlags;
  IMPORTING PermanentData[HV];
  IMPORTING StanyFlags[HV]; 
  IMPORTING StanyData[HV];
  IMPORTING StclearFlags;
  IMPORTING StclearData[HV];


  %% Flags used by the TPM_Startup command				(4.5)
  TPM_STARTUP_TYPE : DATATYPE
  BEGIN
    TPM_ST_CLEAR : TPM_ST_CLEAR?
    TPM_ST_STATE : TPM_ST_STATE?
    TPM_ST_DEACTIVATED : TPM_ST_DEACTIVATED?
  END TPM_STARTUP_TYPE  

  ekInit : (tpmKey?)
  srkInit : (tpmKey?)
  keyGenCntInit : K

  %% Record containing state data to be stored on TPM_SaveState invocation
  %% and restored when using TPM_ST_STATE flag.  The valid? flag indicates
  %% if whether the record is valid or not.  Note that EK and SRK should
  %% technically be in the permData structure, but we like to keep those
  %% values exposed at the TPM state level.  Most presentations of the TPM
  %% do this and do not reference permData or permFlags.  Thus, we try
  %% to be consistent here.
  restoreStateData : TYPE = [#
  		          valid? : bool,
                          srk : (tpmKey?),
		          ek : (tpmKey?),
			  keyGenCnt : K,
			  keys : KEYSET,
                          pcrs : PCRVALUES,
			  permFlags : PermFlags,
			  permData : PermData,
			  stanyFlags: StanyFlags,
			  stanyData: StanyData,
			  stclearFlags: StclearFlags,
			  stclearData: StclearData
			#];


  tpmRestoreStateData : restoreStateData

  tpmRestoreUnknown : restoreStateData = 
    tpmRestoreStateData WITH [`valid?:=FALSE]
   

  wellFormedRestore?(r:restoreStateData) : bool =
    valid?(r) =>
    FORALL (i:PCRINDEX) :
      pcrReset(pcrAttrib(permData(r))(i)) => pcrs(r)(i) = resetOne;


  %% Create a save record with keys, pcrs, and pcr flags.  Throw out pcr
  %% values if they are resettable
  saveState(tpmKeys: KEYSET,
            tpmEK : (tpmKey?),
	    tpmSRK : (tpmKey?),
       	    tpmKeyGenCnt : K,
	    tpmPcrs : PCRVALUES,
	    tpmPermFlags : PermFlags,
	    tpmPermData : PermData,
	    tpmStanyFlags: StanyFlags,
	    tpmStanyData: StanyData,
	    tpmStclearFlags: StclearFlags,
	    tpmStclearData: StclearData ) : (wellFormedRestore?) = 
    (# valid? := TRUE,
       keys := tpmKeys,
       ek := tpmEK,
       srk := tpmSRK,
       keyGenCnt := tpmKeyGenCnt,
       pcrs := (LAMBDA (i:PCRINDEX) :
                     IF pcrReset(pcrAttrib(tpmPermData)(i))
		        THEN resetOne
			ELSE tpmPcrs(i)
                     ENDIF),
       permFlags := tpmPermFlags,
       permData := tpmPermData,
       stanyFlags := tpmStanyFlags,
       stanyData := tpmStanyData,
       stclearFlags := tpmStclearFlags,
       stclearData := tpmStclearData
       #);

  %% Initial value for saved data.  The only field that matters is the valid?
  %% field that must be false.


  initSaveData : (wellFormedRestore?) =
    (# valid? := FALSE,
       keys := (#vals:=emptyset,keys:=emptyset#),
       ek := ekInit,
       srk := srkInit,
       keyGenCnt := keyGenCntInit,
       pcrs := pcrsPower,
       permFlags := permFlagsDefault,
       permData := permDataDefault,	
       stanyFlags:= stanyFlagsDefault,
       stanyData := stanyDataDefault,
       stclearFlags:= stclearFlagsDefault,
       stclearData := stclearDataDefault
    #);

  End startupData
